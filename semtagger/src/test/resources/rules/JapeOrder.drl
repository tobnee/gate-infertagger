#created on: 22.01.2012
package gate.creole.infertagger.test
import gate.creole.infertagger.rulemodel.*;

query before( AnnotationDelegate x, AnnotationDelegate y )
    Space( startOffset==x.endOffset, endOffset==y.startOffset )
end

query order( AnnotationDelegate x, 
			 AnnotationDelegate y, 
			 AnnotationDelegate z )
    before(x, y;)
    before(y, z;)
end

declare Player
	@Marker("MarkRel")	
	name : String @Feature
	surname : String @Feature
	anno : AnnotationDelegate @From
	anno2 : AnnotationDelegate @To
end	

rule "Find player"
    when
    	$player : Word( string == "player" )
    	$name : Word( category == "NNP", orth == "upperInitial" )
    	$surname : Word( category == "NNP", orth == "upperInitial" )
  		order($player, $name, $surname;)  	
    then
        insert(new Player($name.getString(), $surname.getString(), $name, $surname));
end